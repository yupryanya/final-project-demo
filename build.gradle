plugins {
    id 'java'
    id 'io.qameta.allure' version '2.11.2'
    id "io.freefair.lombok" version "6.0.0-m2"
}

group = 'bas-demo'

repositories {
    mavenCentral()
}

def selenideVersion = "6.16.1",
    restAssuredVersion = "5.3.1",
    allureVersion = "2.21.0",
    junitVersion = "5.10.2",
    ownerVersion = "1.0.4",
    slf4jSimpleVersion = "2.0.9",
    assertjVersion = "3.24.2"

compileJava {
    options.encoding = 'UTF-8'
}
compileTestJava {
    options.encoding = 'UTF-8'
}

allure {
    report {
        version.set("2.24.0")
    }
    adapter {
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set("2.24.0")
            }
        }
    }
}

dependencies {
    testImplementation(
            "com.codeborne:selenide:$selenideVersion",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.rest-assured:rest-assured:$restAssuredVersion",
            "io.rest-assured:json-schema-validator:$restAssuredVersion",
            "io.qameta.allure:allure-rest-assured:$allureVersion",
            "org.junit.jupiter:junit-jupiter:$junitVersion",
            "org.slf4j:slf4j-simple:$slf4jSimpleVersion",
            "org.aeonbits.owner:owner:$ownerVersion",
            "org.assertj:assertj-core:$assertjVersion",
            "com.github.javafaker:javafaker:1.0.2"
    )
}

tasks.withType(Test){
    useJUnitPlatform()
    systemProperties(System.getProperties())
    testLogging {
        lifecycle {
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task regression(type: Test) {
    useJUnitPlatform{
        includeTags('regress')
    }
}

task smoke(type: Test) {
    useJUnitPlatform{
        includeTags('smoke')
    }
}